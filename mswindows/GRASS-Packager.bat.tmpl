rem -----------------------------------------------------------------------------------------------------------------------
rem Self Contained GRASS Automated Packager
rem -----------------------------------------------------------------------------------------------------------------------
rem Original author: Marco Pasetti
rem Revised for OSGeo4W by: Colin Nielsen, Helmut Kudrnovsky, and Martin Landa
rem Last Update: $Id$
rem -----------------------------------------------------------------------------------------------------------------------

@echo off

rem --------------------------------------------------------------------------------------------------------------------------
rem Set the script variables
rem --------------------------------------------------------------------------------------------------------------------------

set PACKAGE_DIR=.\GRASS-@GRASS_VERSION_MAJOR@@GRASS_VERSION_MINOR@-Package

set OSGEO4W_DIR=c:\OSGeo4W@OSGEO4W_DIR_POSTFIX@

set GRASS_PREFIX=%OSGEO4W_DIR%\apps\grass\grass-@GRASS_VERSION_MAJOR@.@GRASS_VERSION_MINOR@.@GRASS_VERSION_RELEASE@
set GRASS_BIN_PREFIX=%OSGEO4W_DIR%\bin

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Remove the previous Selected Package and create a new PACKAGE_DIR
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

if exist %PACKAGE_DIR% rmdir /S/Q %PACKAGE_DIR%
mkdir %PACKAGE_DIR%

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy %GRASS_PREFIX% content to PACKAGE_DIR
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

xcopy %GRASS_PREFIX% %PACKAGE_DIR% /S/V/F

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy Extralibs to PACKAGE_DIR\extralib
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

mkdir %PACKAGE_DIR%\extralib

copy %OSGEO4W_DIR%\bin\*.dll %PACKAGE_DIR%\extralib
del %PACKAGE_DIR%\extralib\libgrass_*6.4*.dll
del %PACKAGE_DIR%\extralib\libgrass_*6.5*.dll
del %PACKAGE_DIR%\extralib\Qt*4.dll
del %PACKAGE_DIR%\extralib\msvcp*
del %PACKAGE_DIR%\extralib\msvcr*

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Move GRASS libs from extralib to lib
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

move %PACKAGE_DIR%\extralib\libgrass_*.dll %PACKAGE_DIR%\lib

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy Extrabins to PACKAGE_DIR\extrabin
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

mkdir %PACKAGE_DIR%\extrabin
mkdir %PACKAGE_DIR%\extrabin\gdalplugins

copy %OSGEO4W_DIR%\bin\*.exe %PACKAGE_DIR%\extrabin

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy 'Windows batchfiles for use with R' (GRASS-R-integration) from source to PACKAGE_DIR\extrabin
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

copy %OSGEO4W_DIR%\usr\src\grass_trunk\mswindows\external\rbatch\* %PACKAGE_DIR%\extrabin

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy shared PROJ.4 files to PACKAGE_DIR\proj
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

xcopy %OSGEO4W_DIR%\share\proj %PACKAGE_DIR%\proj /S/V/F/I

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy shared GDAL files to PACKAGE_DIR\share\gdal
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

mkdir %PACKAGE_DIR%\share
mkdir %PACKAGE_DIR%\share\gdal
xcopy %OSGEO4W_DIR%\share\gdal %PACKAGE_DIR%\share\gdal /S/V/F/I

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy shared GEOTIFF files to PACKAGE_DIR\share\epsg_csv
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

mkdir %PACKAGE_DIR%\share\epsg_csv
xcopy %OSGEO4W_DIR%\share\epsg_csv %PACKAGE_DIR%\share\epsg_csv /S/V/F/I

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy MSYS files to PACKAGE_DIR\msys
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

mkdir %PACKAGE_DIR%\msys

copy %OSGEO4W_DIR%\apps\msys\* %PACKAGE_DIR%\msys

xcopy %OSGEO4W_DIR%\apps\msys\bin %PACKAGE_DIR%\msys\bin /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\msys\doc %PACKAGE_DIR%\msys\doc /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\msys\etc %PACKAGE_DIR%\msys\etc /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\msys\info %PACKAGE_DIR%\msys\info /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\msys\lib %PACKAGE_DIR%\msys\lib /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\msys\man %PACKAGE_DIR%\msys\man /S/V/F/I
del %PACKAGE_DIR%\msys\etc\fstab
rem delete msys.bat from osgeo4w because there is an adaption (for spaces in installation path) written by GRASS-Installer.nsi
del %PACKAGE_DIR%\msys\msys.bat

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy Python content to PACKAGE_DIR\Python27
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

mkdir %PACKAGE_DIR%\Python27

copy %OSGEO4W_DIR%\apps\Python27\* %PACKAGE_DIR%\Python27

xcopy %OSGEO4W_DIR%\apps\Python27\DLLs %PACKAGE_DIR%\Python27\DLLs /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\Python27\include %PACKAGE_DIR%\Python27\include /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\Python27\Lib %PACKAGE_DIR%\Python27\Lib /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\Python27\libs %PACKAGE_DIR%\Python27\libs /S/V/F/I
xcopy %OSGEO4W_DIR%\apps\Python27\Scripts %PACKAGE_DIR%\Python27\Scripts /S/V/F/I

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Copy env.bat
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.

copy env.bat %PACKAGE_DIR%\etc

@echo.
@echo -----------------------------------------------------------------------------------------------------------------------
@echo Packaging Completed
@echo -----------------------------------------------------------------------------------------------------------------------
@echo.
