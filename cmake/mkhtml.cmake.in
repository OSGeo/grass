# AUTHOR(S): Rashad Kanavath <rashad km gmail>
# PURPOSE: 	 Create PGM_NAME.html by running mkthml.py. This first set a grass
#             environment and call --html-description on the program.
# COPYRIGHT: (C) 2020 by the GRASS Development Team
#   	    	 This program is free software under the GPL (>=v2)
#   	    	 Read the file COPYING that comes with GRASS for details.
function(fatal_error file_name)
  message(STATUS "ENV{PATH}=$ENV{PATH}")
  message(STATUS "ENV{PYTHONPATH}=$ENV{PYTHONPATH}")
  message(STATUS "ENV{GISRC}=$ENV{GISRC}")
  message(STATUS "ENV{GISBASE}=$ENV{GISBASE}")
  if(UNIX)
    message(STATUS "ENV{LD_LIBRARY_PATH}=$ENV{LD_LIBRARY_PATH}")
  endif()
  message(STATUS "G_TARGET_FILE=${G_TARGET_FILE}")
  execute_process(COMMAND ${CMAKE_COMMAND} -E remove ${G_TARGET_FILE})
  message(SEND_ERROR "Failed to create ${file_name}")
  
endfunction()

if(NOT HTML_FILE)
  
  if(G_TARGET_FILE)
    file(INSTALL ${G_TARGET_FILE} DESTINATION ${OUTPUT_DIR} USE_SOURCE_PERMISSIONS)
  endif()
  return()
endif()

set(GISBASE "@GISBASE@")
set(TOP_DIR "@CMAKE_SOURCE_DIR@")
set(BINARY_DIR "@CMAKE_BINARY_DIR@")
set(PYTHON_EXECUTABLE "@PYTHON_EXECUTABLE@")
file(TO_NATIVE_PATH "${TOP_DIR}" MODULE_TOPDIR )
file(TO_NATIVE_PATH "${GISBASE}" GIS_BASE_DIR )
file(TO_NATIVE_PATH "${GIS_BASE_DIR}/etc/config/rc" GISRC)
file(TO_NATIVE_PATH "${BINARY_DIR}/bin" BIN_DIR)
file(TO_NATIVE_PATH "${GIS_BASE_DIR}/bin" GISBASE_BIN_DIR)
file(TO_NATIVE_PATH "${GIS_BASE_DIR}/scripts" SCRIPTS_DIR)
file(TO_NATIVE_PATH "${GISBASE}/gui/wxpython" WXPYTHON_DIR)
file(TO_NATIVE_PATH "${GISBASE}/etc/python" PYTHON_DIR)

if(WIN32)
  set(P_SEP ";")
else()
  set(P_SEP ":")
endif()

set(ENV{PATH} "${GISBASE_BIN_DIR}${P_SEP}${BIN_DIR}${P_SEP}$ENV{PATH}")
set(ENV{GISBASE} "${GIS_BASE_DIR}")
set(ENV{MODULE_TOPDIR} "${MODULE_TOPDIR}")
set(ENV{GISRC} "${GISRC}")
set(ENV{LC_ALL} C)
set(ENV{LANG} C)
set(ENV{LANGUAGE} C)
if(UNIX)
  set(GRASS_LD_LIBRARY_PATH "${BINARY_DIR}/lib:$ENV{LD_LIBRARY_PATH}")
  string(REGEX REPLACE "^(.*)\\${P_SEP}$" "\\1" GRASS_LD_LIBRARY_PATH "${GRASS_LD_LIBRARY_PATH}")
  set(ENV{LD_LIBRARY_PATH} ${GRASS_LD_LIBRARY_PATH})
endif()

set(GRASS_PYTHONPATH "${WXPYTHON_DIR}${P_SEP}${PYTHON_DIR}${P_SEP}$ENV{PYTHONPATH}")
string(REGEX REPLACE "^(.*)\\${P_SEP}$" "\\1" GRASS_PYTHONPATH "${GRASS_PYTHONPATH}")
set(ENV{PYTHONPATH} "${GRASS_PYTHONPATH}")

get_filename_component(HTML_FILE_NAME ${HTML_FILE} NAME)
get_filename_component(PGM_SOURCE_DIR ${HTML_FILE} PATH)

string(REPLACE ".html"  "" PGM_NAME "${HTML_FILE_NAME}" )

string(REPLACE ".html" ".tmp.html" TMP_HTML_NAME ${HTML_FILE_NAME})
set(TMP_HTML_FILE ${PGM_SOURCE_DIR}/${TMP_HTML_NAME})
set(OUT_HTML_FILE ${GISBASE}/docs/html/${HTML_FILE_NAME})
set(MKHTML_PY ${BINARY_DIR}/tools/mkhtml.py)

if(RUN_HTML_DESCR)
  set(PGM_EXT "")
  if(WIN32)
    set(PGM_EXT ".exe")
  endif()
  
  if(PYTHON_SCRIPT)
    set(PGM_EXT ".py")
    set(launcher "${PYTHON_EXECUTABLE}")
  else()
    set(launcher "")
  endif()
  #message("Running ${PGM_SOURCE_DIR} ${launcher} ${PGM_NAME}${PGM_EXT} --html-description" )
  execute_process(
    COMMAND ${CMAKE_COMMAND} -E chdir ${PGM_SOURCE_DIR} ${launcher} ${PGM_NAME}${PGM_EXT} "--html-description"
    OUTPUT_FILE ${TMP_HTML_FILE}
    RESULT_VARIABLE html_descr_rv)

  #TODO: print TMP_HTML_FILE contents in case of failure.
  if(NOT html_descr_rv EQUAL 0)
    message("Running html-description says ${html_descr_rv}")
    fatal_error(${TMP_HTML_FILE})
  endif()

  if(NOT EXISTS ${TMP_HTML_FILE})
    fatal_error(${TMP_HTML_FILE})
  endif()
endif(RUN_HTML_DESCR)

message(STATUS "Creating ${OUT_HTML_FILE}")
execute_process(
  COMMAND ${CMAKE_COMMAND} -E chdir ${PGM_SOURCE_DIR} ${PYTHON_EXECUTABLE} ${MKHTML_PY} ${PGM_NAME}
  OUTPUT_FILE ${OUT_HTML_FILE}
  RESULT_VARIABLE mkhtml_rv)

#message("mkhtml_rv=${mkhtml_rv}")
if(NOT mkhtml_rv EQUAL 0)
  fatal_error(${OUT_HTML_FILE})
endif()

file(GLOB IMG_FILES ${PGM_SOURCE_DIR}/*.png  ${PGM_SOURCE_DIR}/*.jpg)
foreach(IMG_FILE ${IMG_FILES})
  file(COPY ${IMG_FILE} DESTINATION ${GISBASE}/docs/html)
endforeach()

if(PYTHON_SCRIPT)
  if(WIN32)
    file(INSTALL ${G_TARGET_FILE} DESTINATION ${OUTPUT_DIR} USE_SOURCE_PERMISSIONS)
    configure_file(${TOP_DIR}/cmake/py_cli_install.bat.in  ${OUTPUT_DIR}/${PGM_NAME}.bat @ONLY)
  else(WIN32)
    file(INSTALL ${G_TARGET_FILE} DESTINATION ${OUTPUT_DIR}
      RENAME ${PGM_NAME}
      FILE_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE)
  endif(WIN32)
else(PYTHON_SCRIPT)
    file(INSTALL ${G_TARGET_FILE} DESTINATION ${OUTPUT_DIR} USE_SOURCE_PERMISSIONS)  
endif(PYTHON_SCRIPT)


file(REMOVE  ${TMP_HTML_FILE})

